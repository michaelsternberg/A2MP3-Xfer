10 REM USE A2MP3 CARD IN SLOT 3 TO 
20 REM XFER DOS 3.3 FLOPPIES FROM/TO USB
30 REM RETROCHALLENGE 2014 WINTER WARMUP
40 REM VERSION 1.0 19-JAN-2014

100 GOSUB 2000 : REM "DEFINE_CALLS"
110 GOSUB 1400 : REM "RWTS_GETIOB"
120 GOSUB 1200 : REM "INIT_6551"
130 LET SL = 6 : LET DR = 1

200 REM "MAIN_LOOP"
210     POKE SKIPRD, 0
220     GOSUB 300 : REM "SHOW_MENU"
230     IF CH$ =  "1" THEN GOSUB 500 : REM "XFER_FLOPPY"
240     IF CH$ =  "2" THEN GOSUB 900 : REM "XFER_USB"
250     IF CH$ =  "3" THEN GOSUB 2100 : REM "FORMAT_FLOPPY"
260     IF CH$ =  "4" THEN GOSUB 2700 : REM "ENTER_CMD"
270     IF CH$ =  "5" THEN GOSUB 2200 : REM "HELP"
280     IF CH$ <> "6" THEN GOTO 200
290 END

300 REM "SHOW_MENU"
310 HOME
320 PRINT "======================================="
330 PRINT "               MAIN MENU               "
340 PRINT "======================================="
350 PRINT "[1] TRANSFER FLOPPY TO A2MP3"
360 PRINT "[2] TRANSFER A2MP3 TO FLOPPY"
370 PRINT "[3] FORMAT FLOPPY"
380 PRINT "[4] ENTER VINCULUM COMMAND"
390 PRINT "[5] HELP"
400 PRINT "[6] QUIT"
410 PRINT "======================================="
420 INPUT "ENTER CHOICE:";CH$
430 RETURN

500 REM "XFER_FLOPPY"
510 HOME
520 PRINT "======================================="
530 PRINT "       TRANSFER FLOPPY TO A2MP3"
540 PRINT "======================================="
550 INPUT "ENTER FILENAME (8.3):";FL$
560 PRINT "IS "; FL$; " CORRECT (Y/<N>)";: INPUT R$
570 IF R$ <> "Y" AND R$ <> "y" THEN RETURN
580 PRINT "INSERT "; : FLASH : ? "SOURCE DISK"; : NORMAL : ? " AND PRESS ENTER" : INPUT R$
590 HOME
600 PRINT "======================================="
610 PRINT "     TRANSFERRING FLOPPY TO A2MP3"
620 PRINT "======================================="
630 GOSUB 1900 : REM "DRAW_GRID"
640 GOSUB 1500 : REM "RWTS_INIT"
650 LET V$ = "OPW " + FL$   : GOSUB 1300 : REM "SEND_VIN_CMD"
660 LET TX = 3: LET TY = 6
670 POKE SKIPRD, 1
680 FOR TR = 0 TO 34
690     LET V$ = "WRF 4096"   : GOSUB 1300 : REM "SEND_VIN_CMD"
700     FOR SE = 0 TO 15
710         HTAB TX : VTAB TY : PRINT "*"
720         GOSUB 1600 : REM "RWTS_READ_SECTOR"
730         CALL S256                   : REM SEND256
740         HTAB TX : VTAB TY : PRINT CHR$(255)
750         LET TX = TX + 1
760         IF TX > 37 THEN TX = 3 : TY = TY + 1
770     NEXT SE
780 NEXT TR
790 LET V$ = "CLF" : GOSUB 1300 : REM "SEND_VIN_CMD"
800 POKE SKIPRD, 0
810 RETURN

900 REM "XFER_USB"
910 HOME 
920 PRINT "======================================="
930 PRINT "       TRANSFER A2MP3 TO FLOPPY"
940 PRINT "======================================="
950 INPUT "ENTER FILENAME (8.3):";FL$
960 PRINT "IS "; FL$; " CORRECT (Y/<N>)"; : INPUT R$
970 IF R$ <> "Y" AND R$ <> "y" THEN RETURN
980 PRINT "INSERT "; : FLASH : ? "TARGET DISK"; : NORMAL : ? " AND PRESS ENTER" : INPUT R$
990 HOME 
1000 PRINT "======================================="
1010 PRINT "     TRANSFERRING A2MP3 TO FLOPPY"
1020 PRINT "======================================="
1030 GOSUB 1900 : REM "DRAW_GRID"
1040 GOSUB 1500 : REM "RWTS_INIT"
1050 LET V$ = "OPR " + FL$  : GOSUB 1300 : REM "SEND_VIN_CMD"
1060 LET TX = 3 : LET TY = 6
1070 FOR TR = 0 TO 34
1080    FOR SE = 0 TO 15
1090        LET V$ = "RDF 256" : GOSUB 1300 : REM "SEND_VIN_CMD"
1100        HTAB TX : VTAB TY : PRINT "*"
1110        GOSUB 1700 : REM "RWTS_WRITE_SECTOR"
1120        HTAB TX : VTAB TY : PRINT CHR$(255)
1130        LET TX = TX + 1
1140        IF TX > 37 THEN TX = 3 : TY = TY + 1
1150     NEXT SE
1160 NEXT TR
1170 LET V$ = "CLF" : GOSUB 1300 : REM "SEND_VIN_CMD"
1180 RETURN
  
1200 REM "INIT_6551"
1210 PRINT "INFO> INITIALIZING 6551"
1220 CALL I6551
1230 FOR I = 1 TO 250: NEXT I           : REM WAIT FOR INIT
1250 RETURN

1300 REM "SEND_VIN_CMD"
1310 FOR I = 1 TO LEN(V$)
1320 LET CH = ASC (MID$ (V$, I, 1))
1330 POKE CMD + I - 1, CH
1340 NEXT I
1350 POKE CMD + I - 1, 13       : REM CR ($0D)
1360 POKE CMD + I, 0            : REM NULL ($00)
1370 CALL SSTR
1380 FOR I = 1 TO 25 : NEXT I   : REM SMALL WAIT FOR VMUSIC2
1390 RETURN

1400 REM "RWTS_GETIOB"
1410 PRINT "INFO> GETTING IO BLOCK"
1420 CALL GIOB                  : REM GETIOB
1430 LET IOB = PEEK(253) + 256 * PEEK(254)
1440 RETURN

1500 REM "RWTS_INIT"
1510 POKE IOB + 0, 1             : REM TABLE TYPE (ALWAYS $01)
1520 POKE IOB + 1, SL * 16       : REM SLOT NUMBER (TIMES 16)
1530 POKE IOB + 2, DR            : REM DRIVE NUMBER
1540 POKE IOB + 3, 0             : REM NO EXPECTED VOLUME
1550 POKE IOB + 8, 0             : REM (LSB OF $2100 - 256 BYTE BUF FOR RD/WR)
1560 POKE IOB + 9, 2 * 16 + 1    : REM (MSB OF $2100)
1570 RETURN

1600 REM "RWTS_READ_SECTOR"
1610 HTAB 12 : VTAB 5 : PRINT "TRACK:"; TR; " SECTOR:"; SE; " "
1620 POKE IOB + 4, TR            : REM SET TRACK
1630 POKE IOB + 5, SE            : REM SET SECTOR
1660 POKE IOB + 11, 0            : REM GET WHOLE SECTOR
1670 POKE IOB + 12, 1            : REM SET CMD TO READ
1680 CALL RWTS                   : REM CALLRWTS ($2070)
1690 RETURN

1700 REM "RWTS_WRITE_SECTOR"
1710 HTAB 12 : VTAB 5 : PRINT "TRACK:"; TR; " SECTOR:"; SE; " "
1720 POKE IOB + 4, TR            : REM SET TRACK
1730 POKE IOB + 5, SE            : REM SET SECTOR
1760 POKE IOB + 11, 0            : REM GET WHOLE SECTOR
1770 POKE IOB + 12, 2            : REM SET CMD TO READ
1780 CALL RWTS                   : REM CALLRWTS
1790 RETURN

1800 REM "RWTS_FORMAT"
1810 PRINT "INSERT "; : FLASH : ? "BLANK DISK"; : NORMAL : ? " AND PRESS ENTER" : INPUT R$
1820 POKE IOB + 1, SL * 16
1830 POKE IOB + 2, DR
1840 POKE IOB + 3, 0
1850 POKE IOB + 12, 4            : REM SET CMD TO FORMAT
1860 CALL RWTS
1870 PRINT "RETURN CODE: "; PEEK (IOB + 13)
1880 RETURN

1900 REM "DRAW_GRID"
1910 FOR Y = 0 TO 15
1920    HTAB 3 : VTAB Y + 6 
1930    PRINT "..................................."
1940 NEXT Y
1950 
1960 RETURN

2000 REM "DEFINE_CALLS"
2010 PRINT "INFO> ASSIGNING CALLS"
2020 LET I6551 = 8192 : REM INIT6551 ($2000)
2030 LET SSTR  = 8219 : REM SENDST   ($201B)
2040 LET S256  = 8257 : REM SEND256  ($2041)
2050 LET R256  = 8268 : REM READ256  ($204C)
2060 LET GIOB  = 8308 : REM GETIOB   ($2074)
2070 LET RWTS  = 8316 : REM CALLRWTS ($207C)
2080 LET CMD   = 8416 : REM CMD BUFFER ($20E0)
2085 LET SKIPRD = 8414 : REM SET TO NONZERO TO SKIP R256 ($20DE)
2090 RETURN

2100 REM "FORMAT_FLOPPY"
2110 HOME
2120 PRINT "======================================="
2130 PRINT "            FORMAT FLOPPY"
2140 PRINT "======================================="
2150 PRINT "FORMAT (Y/<N>)"; : INPUT R$
2160 IF R$ = "Y" OR R$ = "y" THEN GOSUB 1800 : REM "RWTS_FORMAT"
2170 RETURN

2200 REM "HELP"
2210 HOME
2220 PRINT "---------------------------------------"
2230 PRINT "         A2MP3-XFER VERSION 1.0"
2240 PRINT "          BY MICHAEL STERNBERG"
2250 PRINT "   RETROCHALLENGE 2014 WINTER WARMUP"
2260 PRINT "---------------------------------------"
2270 PRINT "PURPOSE: TRANSFER DOS 3.3 DISK IMAGES"
2280 PRINT "TO/FROM USB DRIVE ON BRIEL'S A2MP3 CARD"
2290 PRINT
2300 PRINT "CURRENT LIMITATIONS:"
2310 PRINT "* A2MP3 MUST BE IN INSTALLED IN SLOT 3"
2320 PRINT "* FLOPPY MUST BE IN SLOT 6 DRIVE 1"
2330 PRINT "* DOS 3.3 DISK IMAGES ONLY"
2340 PRINT "* .DSK IMAGES ONLY (8.3 MSDOS-STYLE)"
2350 PRINT "* USE AT OWN RISK"
2360 PRINT
2370 PRINT "TRANSFER FLOPPY TO A2MP3:"
2380 PRINT "1. ENTER FILENAME TO BE CREATED ON USB"
2390 PRINT "2. INSERT SOURCE DISK IN SLOT 6 DRIVE 1"
2400 PRINT 
2410 PRINT "TRANSFER A2MP3 TO FLOPPY:"
2420 PRINT "1. ENTER EXISTING .DSK FILENAME ON USB"
2430 PRINT "2. INSERT TARGET DISK IN SLOT 6 DRIVE 1"
2440 PRINT
2450 PRINT "              HIT ANY KEY";
2460 CALL -756
2470 HOME
2480 PRINT "---------------------------------------"
2490 PRINT "              A2MP3-XFER"
2500 PRINT "---------------------------------------"
2510 PRINT "FORMAT FLOPPY:"
2520 PRINT "1. INSERT BLANK DISK IN SLOT 6 DRIVE 1"
2530 PRINT
2540 PRINT "ENTER VINCULUM COMMAND:"
2550 PRINT "1. ENTER CMD UNDERSTOOD BY VMUSIC2"
2560 PRINT "   FIRMWARE, SUCH AS FWV OR V3A"
2570 PRINT "   OUTPUT > 256 CHARS IS UNDEFINED"
2580 PRINT
2590 PRINT "              HIT ANY KEY";
2600 CALL -756
2610 RETURN
  
2700 REM "ENTER_CMD"
2710 HOME : PRINT "PLEASE WAIT..."
2720 FOR I = 0 TO 255 : POKE 8448 + I, 0 : NEXT I
2730 HOME : PRINT "ENTER CMD"; : INPUT V$ : GOSUB 1300
2740 FOR I = 0 TO 255
2750    X = PEEK (8448 + I) : IF X = 0 THEN I = 255
2760    PRINT CHR$(X);
2770 NEXT I
2780 HTAB 15 : VTAB 23 : PRINT "HIT ANY KEY"; : CALL -756
2790 RETURN
